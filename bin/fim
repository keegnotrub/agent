#!/usr/bin/env zsh

# Usage: fim --help
# Requires: cat, curl, jq, printf, read

CURSOR="${1}"

case "${CURSOR}" in
    "" | "-h" | "--help")
        echo "usage: fim <cursor>"
        echo "\nUse cursor to provide the position for completion within text."
        echo "\nText should be piped to fim for completion."
        echo "   $ cat app/models/user.rb | fim 732"
        exit 0
        ;;
    *[!0-9]*)
        echo "fim: '${CURSOR}' is not a fim cursor. See 'fim --help'."
        exit 2
        ;;
esac

if [[ ! -t 0 ]]; then
    read -u 0 -k $CURSOR PROMPT
    SUFFIX=$(cat)
else
    echo "fim: 'requires text piped for completion. See 'fim --help'."
    exit 2
fi

curl -s -N \
  --location 'http://localhost:11434/api/generate' \
  --header 'Content-Type: application/json' \
  --header 'Accept: application/json' \
  --data "{
  \"model\": \"codestral\",
  \"stream\": true,
  \"prompt\": $(printf %s ${PROMPT} | jq -Rs '.'),
  \"suffix\": $(printf %s ${SUFFIX} | jq -Rs '.'),
  \"options\": {
    \"temperature\": 0,
    \"stop\": [
      \"\\n\\n\",
      \"\\nend\",
      \"\\n  end\",
      \"\\n    end\",
      \"\\n      end\"
    ]
  }
}" | jq --unbuffered -Rrj \
        'fromjson|.response|if input_line_number == 1 then ltrimstr(" ") else . end'
